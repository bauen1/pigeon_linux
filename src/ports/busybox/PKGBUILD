pkgname=busybox
pkgver=1.26.2
pkgrel=1
pkgdesc="A statically linked busybox"
arch=('x86_64')
url="http://busybox.net/"
license=('GPL')
depends=('glibc')
options=('!buildflags')
source=(http://busybox.net/downloads/busybox-$pkgver.tar.bz2)
sha256sums=('SKIP')

prepare() {
  cd "busybox-$pkgver"
  make defconfig
  sed -i 's/.*CONFIG_STATIC.*/CONFIG_STATIC=y/g' .config
}

build() {
  make -C "busybox-$pkgver"
}

package() {
  cd "$srcdir/busybox-$pkgver"
  install -D -m755 busybox "$pkgdir"/bin/busybox
  install -D -m644 docs/busybox.1 "$pkgdir"/usr/share/man/man1/busybox.1
  install -D -m644 docs/BusyBox.html "$pkgdir"/usr/share/doc/$pkgname/BusyBox.html
  install -D -m644 docs/BusyBox.txt "$pkgdir"/usr/share/doc/$pkgname/BusyBox.txt
}
